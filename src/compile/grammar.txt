Goal            : Content $
Content(next)   : Content Node
Content(first)  : Node

Node    : Element
Node    : Tag
Node    : Text

Element                     : .LT ElementCode
ElementCode(begin)          : ElementBeginContent ElementEndMark
ElementCode(end)            : .SLASH ElementNameWS .GT
ElementEndMark(slash)       : .SLASH .GT
ElementEndMark              : .GT
ElementBeginContent(attr)   : ElementName HtmlAttributes
ElementBeginContent         : ElementName
ElementNameWS               : ElementName .ws
ElementNameWS               : ElementName
ElementName                 : HtmlName
HtmlAttributes(list)        : HtmlAttributes HtmlAttributeWS
HtmlAttributes(init)        : .ws
HtmlAttributeWS             : HtmlAttribute .ws
HtmlAttributeWS             : HtmlAttribute
HtmlAttribute(Value)        : HtmlName HtmlAttributeEqValue
HtmlAttribute(Bool)         : HtmlName
HtmlAttributeEqValue        : .EQ HtmlAttributeValue
HtmlAttributeValue(Expr)    : TagExpression
HtmlAttributeValue          : HtmlQQString
HtmlAttributeValue          : HtmlQString
HtmlAttributeValue(Plain)   : HtmlPlainValue

HtmlName(ns)        : HtmlSimpleName .COLON HtmlSimpleName
HtmlName            : HtmlSimpleName
HtmlSimpleName(d)   : HtmlSimpleName .DASH HtmlNameWord
HtmlSimpleName      : HtmlNameWord

HtmlQQString        : .QQ HtmlQQContent .QQ
HtmlQQString(empty) : .QQ .QQ
HtmlQQContent(loop) : HtmlQQContent HtmlQQContentPart
HtmlQQContent(first): HtmlQQContentPart
HtmlQQContentPart   : HtmlQQText
HtmlQQContentPart(E): TagExpression
HtmlQQText(loop)    : HtmlQQText HtmlQQTextPart
HtmlQQText(first)   : HtmlQQTextPart
HtmlQQTextPart      : HtmlFlowText
HtmlQQTextPart      : HtmlQQTextPartSpec

HtmlQString         : .Q HtmlQContent .Q
HtmlQString(empty)  : .Q .Q
HtmlQContent(loop)  : HtmlQContent HtmlQContentPart
HtmlQContent(first) : HtmlQContentPart
HtmlQContentPart    : HtmlQText
HtmlQContentPart(E) : TagExpression
HtmlQText(loop)     : HtmlQText HtmlQTextPart
HtmlQText(first)    : HtmlQTextPart
HtmlQTextPart       : HtmlFlowText
HtmlQTextPart       : HtmlQTextPartSpec



Text(empty)         : .EolWithWs
Text                : .EolWithWs InlineTextWithEolWs
Text                : InlineTextWithEolWs
InlineTextWithEolWs : InlineText .EolWithWs
InlineTextWithEolWs : InlineText

Tag                     : .TagStart TagContinueAny
TagExpression           : .TagStart WsTagExpressionContinue
TagContinueAny(comment) : .CommentTagContinue
TagContinueAny          : WsTagContinue
CommentTagContinue      : CommentTagSharp .CommentTagContent CommentTagEnd
CommentTagContinue      : CommentTagSharp CommentTagEnd
WsTagContinue           : .ws TagContinue
WsTagContinue           : TagContinue
TagContinue(Empty)      : .TagEnd
TagContinue(Expr)       : TagExpressionContinue
TagContinue(St)         : .StatementMark StatementContinue
WsTagExpressionContinue : .ws TagExpressionContinue
WsTagExpressionContinue : TagExpressionContinue
TagExpressionContinue   : Expression .WsTagEnd
StatementContinue(I)    : InlineStatementContinue
InlineStatementContinue : InlineStatement .WsTagEnd
WsTagEnd                : .ws TagEnd
WsTagEnd                : TagEnd
TagStart        : "{{"
TagEnd          : "}}"
CommentTagSharp : "#"
CommentTagEnd   : "#}}"
StatementMark   : "%"

InlineStatement : name

Expression  : Variable

Variable    : "$" name

LT      : "<"
GT      : ">"
SLASH   : "/"
EQ      : "="
COLON   : ":"
DASH    : "-"
QQ      : <">
Q       : <'>
ws      : /\s++/

HtmlNameWord        : /(?i)[a-z][a-z0-9]*+/
name                : /(?i)[a-z_][a-z_0-9]*+/
InlineText          : /(?:[^\s<>{}]++|\h++(?!\z|\R)|\{(?!\{)|\}(?!\}))++/
CommentTagContent   : /(?:[^#]++|#(?!\}\}))++/
HtmlQQTextPartSpec  : /[<>']++/
HtmlQTextPartSpec   : /[<>"]++/
EolWithWs           : /\h*+(?:\R\h*+)++/
HtmlPlainValue      : /[^<>'"={}\s]++/
HtmlFlowText        : /[^<>'"{}]++/
